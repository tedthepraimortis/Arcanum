class VoidstormSpell : ArcanumSpell
{
	override int GetIndex() { return 50; }
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeDestruction'; }
	override string GetName() { return "Voidstorm"; }
	override int GetSpellType() { return SType_Targeted; }
	override int GetManaCost() { return 200; }
	override string GetDescription() { return "Launches forward a black hole that sucks enemies in and removes them from existence. It explodes once it comes into contact with a hard surface."; }
	override string GetTechnicalInfo() { return "Mass, Dangerous"; }
	override int GetMaxExperience() { return 0; }
	override class<ArcanumSigil> GetSigil() { return 'VoidstormSigil'; }
}

class VoidstormSigil : ArcanumSigil
{
	override void ActivateTargeted()
	{
		PlayCastSound(self, "Arcanum/Destruction/Voidstorm/Start");
		Actor a = Spawn("VoidstormOrb", pos);
		a.angle = angle;
		a.pitch = pitch;
		a.target = master;
		a.A_ChangeVelocity(4 * cos(pitch), 0, 4 * -sin(pitch), CVF_RELATIVE);
	}

	override void InitRuneSlots()
	{
		vector2 BigSize = (13, 21.66);
		CreateRuneSlot((0, 0), BigSize, 4);

		vector2 SmallSize = (5.5, 9.16);
		CreateRuneSlot((-33.1, 0), SmallSize, 1.5);
		CreateRuneSlot((33.1, 0), SmallSize, 1.5);
	}

	Default
	{
		Height 44;
		Radius 50;
		StencilColor "835FB4";
	}

	States
	{
		Spawn:
			CRDE F 0;
			Goto Super::Spawn;
	}
}

class VoidstormOrb : Actor
{
	Default
	{
		Radius 10;
		Height 17;
		+MISSILE
		+NOGRAVITY
		+THRUACTORS
		+ROLLSPRITE
		+ROLLCENTER
		+NOTRIGGER
		+FORCEXYBILLBOARD
		Renderstyle "Subtract";
		Alpha 0.3;
		Scale 0.4;
	}

	const EffectRange = HDCONST_ONEMETRE * 12;

	States
	{
		Spawn:
			VSTR M 0 NoDelay A_StartSound("Arcanum/Destruction/Voidstorm/Loop", 10, CHANF_LOOP, pitch: 0.7);
			VSTR M 1
			{
				A_SetScale(frandom(0.40, 0.43));
				roll -= 4;
				BlockThingsIterator it = BlockThingsIterator.Create(self, EffectRange);
				while (it.Next())
				{
					string Follower = "HDFollower";
					class<Actor> FollowerCls = Follower;
					if (!it.thing.bISMONSTER || it.thing.bNODAMAGE || !CheckSight(it.thing, SF_IGNOREVISIBILITY) || it.thing is 'HDPlayerCorpse' || it.thing is FollowerCls)
					{
						continue;
					}

					if (it.thing.Health <= 0 && Distance3D(it.thing) < 48)
					{
						it.thing.Destroy();
						continue;
					}

					if (it.thing.Health > 0)
					{
						it.thing.DamageMobj(self, target, 10, 'None', DMG_FORCED);
					}

					if (it.thing)
					{
						it.thing.bSLIDESONWALLS = true;
						it.thing.A_Face(self, 0, 0);
						it.thing.A_ChangeVelocity(1 * cos(it.thing.pitch), 0, 2 * -sin(it.thing.pitch), CVF_RELATIVE);
						it.thing.vel.x = clamp(it.thing.vel.x, -8, 8);
						it.thing.vel.y = clamp(it.thing.vel.y, -8, 8);
						it.thing.vel.z = clamp(it.thing.vel.z, -8, 8);
					}
				}

				double OldAngle = angle;
				double OldPitch = pitch;

				if (!random(0, 1))
				{
					angle = frandom(0, 359);
					pitch = frandom(-90, 90);
					A_CustomRailgun(0, 0, "", 0x15031C, RGF_SILENT | RGF_EXPLICITANGLE, 0, 10, "VoidstormOrbPuff", range: EffectRange, duration: 15);
				}

				angle = OldAngle;
				pitch = OldPitch;

				if (GetAge() % 3 == 0)
				{
					A_SpawnItemEx("VoidstormOrbTrail", flags: SXF_NOCHECKPOSITION | SXF_TRANSFERROLL);
				}
			}
			Wait;
		Death:
			TNT1 A 20
			{
				for (int i = 0; i < 4; ++i)
				{
					Actor BSpot = Spawn("BeamSpot", pos, ALLOW_REPLACE);
					BSpot.target = self;
					BSpot.stamina = 144;
				}

				// Horizontal ring.
				for (int i = -180; i < 180; i += 2)
				{
					A_SpawnParticle(Color(0x401c4f), SPF_FULLBRIGHT | SPF_RELATIVE, 12, 96, i, 0, 0, 0, 24, 0, 0);
				}
				
				// Ball.
				for (int i = -180; i < 180; i += 6)
				{
					for (int j = -90 + 6; j < 90; j += 6)
					{
						A_SpawnParticle(Color(0x15031c), SPF_FULLBRIGHT | SPF_RELATIVE, 8, 64, i, 0, 0, 0, 14 * cos(j) * 1.2, 0, 14 * sin(j));
					}
				}
			}
			Stop;
	}
}
class VoidstormOrbTrail : Actor
{
	Default
	{
		+NOINTERACTION
		+FORCEXYBILLBOARD
		Renderstyle "Subtract";
		Scale 0.30;
		Alpha 0.1;
	}

	States
	{
		Spawn:
			VSTR M 1
			{
				A_SetScale(Scale.x - 0.002);
				A_FadeOut(0.001);
			}
			Wait;
	}
}

class VoidstormOrbPuff : Actor
{
	Default
	{
		+BLOODLESSIMPACT
	}

	States
	{
		Spawn:
			TNT1 A 1;
			Stop;
	}
}