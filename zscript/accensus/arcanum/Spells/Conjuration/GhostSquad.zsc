class GhostSquadSpell : ArcanumSpell
{
	override int GetIndex() { return 5; }
	override class<ArcanumSpellTree> GetTree() { return 'ArcanumTreeConjuration'; }
	override string GetName() { return "Ghost Squad"; }
	override int GetSpellType() { return SType_Targeted; }
	override int GetManaCost() { return 80; }
	override string GetDescription()
	{
		int count = int(1 + 2 * GetExperienceFactor());
		return String.Format(loc("$CONJURATION_GHOSTSQUAD_DESC"), count, count > 1 ? loc("$CONJURATION_GHOSTSQUAD_DESC2") : "");
	}
	override string GetTechnicalInfo() { return loc("$CONJURATION_GHOSTSQUAD_INFO"); }
	override int GetMaxExperience() { return 60; }
	override class<ArcanumSigil> GetSigil() { return 'GhostSquadSigil'; }
}

class GhostSquadSigil : ArcanumSigil
{
	override void ActivateTargeted()
	{
		FLineTraceData Data;
		LineTrace(angle, HDCONST_ONEMETRE * 100, pitch, 0, 0, 0, 0, Data);

		vector3 OldPos = master.pos;
		if (Data.HitType != Data.TRACE_HasHitSky)
		{
			master.SetXYZ(Data.HitLocation - Data.HitDir);
			PlayCastSound(self, "Arcanum/Conjuration/GhostSquad");
			master.A_AlertMonsters();

			int count = int(1 + 5 * ExperienceFactor);
			for (int i = 0; i < count; ++i)
			{
				double ang = (360.0 / count) * i;
				master.A_SpawnItemEx("SquadGhost", 0, 0, 0, -8, 0, 0, ang, SXF_NOCHECKPOSITION | SXF_SETMASTER);
				master.A_SpawnItemEx("HDSmoke", 0, 0, 0, -8, 0, 0, ang, SXF_NOCHECKPOSITION);
			}

			string deadawaken;
			switch (random(0, 3))
			{
				case 0: deadawaken = Stringtable.Localize("$CONJURATION_GHOSTSQUAD_ACTIVATE1"); break;
				case 1: deadawaken = Stringtable.Localize("$CONJURATION_GHOSTSQUAD_ACTIVATE2"); break;
				case 2: deadawaken = Stringtable.Localize("$CONJURATION_GHOSTSQUAD_ACTIVATE3"); break;
				case 3: deadawaken = Stringtable.Localize("$CONJURATION_GHOSTSQUAD_ACTIVATE4"); break;
			}
			A_PrintBold(deadawaken, deadawaken.length() * 0.05, "NewSmallFont");
			master.SetXYZ(OldPos);
		}
	}

	override void InitRuneSlots()
	{
		vector2 Size = (2, 3.33);
		CreateRuneSlot((-8.4, 0), Size, 0.5); CreateRuneSlot((8.4, 0), Size, 0.5);
		CreateRuneSlot((0, -8.4), Size, 0.5); CreateRuneSlot((0, 8.4), Size, 0.5);
	}

	Default
	{
		StencilColor "B4C8A3";
	}

	States
	{
		Spawn:
			CRCN A 0;
			Goto Super::Spawn;
	}
}